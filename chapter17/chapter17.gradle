subprojects {
	group 'com.apress.prospring5.ch17'
	apply plugin: 'java'

	//Unfortunately, the runtime discovery algorithm in commons-logging, while convenient for the end-user, is problematic
	// So the Spring team recommends excluding it and replacing it with SL4j
	// https://docs.spring.io/spring/docs/5.0.0.M4/spring-framework-reference/htmlsingle/#overview-not-using-commons-logging
	configurations {
		all*.exclude group: "commons-logging", module: "commons-logging"
	}

	/*Task that copies all the dependencies under build/libs */
	task copyDependencies(type: Copy) {
		from configurations.compile
		into 'build/libs'
	}

	dependencies {
		if (!project.name.contains("boot")) {
			compile (web.tomcatWsApi) {
				exclude  module: 'tomcat-embed-core'
			}
			compile (web.tomcatWsEmbed) {
				exclude  module: 'tomcat-embed-core'
			}
			compile spring.context, spring.webSocket, spring.messaging, spring.webmvc, web.websocket, misc.slf4jJcl,
					misc.logback, misc.lang3, misc.jacksonDatabind, web.servlet, web.jstl
		}
		testCompile testing.junit
	}
}
subprojects {
	group 'com.apress.prospring5.ch12'
	apply plugin: 'java'

	//Unfortunately, the runtime discovery algorithm in commons-logging, while convenient for the end-user, is problematic
	// So the Spring team recommends excluding it and replacing it with SL4j
	// https://docs.spring.io/spring/docs/5.0.0.M4/spring-framework-reference/htmlsingle/#overview-not-using-commons-logging
	configurations {
		all*.exclude group: "commons-logging", module: "commons-logging"
	}

	/*Task that copies all the dependencies under build/libs */
	task copyDependencies(type: Copy) {
		from configurations.compile
		into 'build/libs'
	}

	dependencies {
		//we specify these dependencies for all submodules, except the boot module, that defines its own
		if (!project.name.contains("boot")) {
			compile (spring.contextSupport) {
				exclude  module: 'spring-context'
				exclude  module: 'spring-beans'
				exclude  module: 'spring-core'
			}
			compile misc.slf4jJcl, misc.logback, misc.lang3, hibernate.em, misc.guava
		}
		testCompile testing.junit
	}
}